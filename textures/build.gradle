// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

description = 'generate texture assets used by SkyControl'
ext {
    bsc = 'src/main/resources/bsc5.dat'
    skies = '../SkyControl/src/main/resources/Textures/skies'
}

clean {
    dependsOn 'cleanCopySource'
    dependsOn 'cleanTextures'
}
compileJava { dependsOn 'copySource' }

dependencies {
    compile "com.beust:jcommander:$jcommanderVersion"
    compile "org.jmonkeyengine:jme3-core:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-bullet:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-effects:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-terrain:$jmonkeyengineVersion"

    //compile "jme3utilities:jme3-utilities-heart:$jme3utilitiesheartVersion"
    compile project(':heart')
}

tasks.withType(JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
}

task copySource(type: Copy) {
    from('../SkyControl/src/main/java/jme3utilities') {
        include 'sky/Constants.java', 'sky/DomeMesh.java', 'sky/LunarPhase.java'
    }
    into 'src/main/java/jme3utilities'
}
task cleanCopySource(type: Delete) {
    delete fileTree(dir: 'src/main/java', exclude: '**/textures/**')
}

task textures {
    dependsOn = ['clouds', 'equator', 'equator16m', 'moons', 'north', \
        'north16m',  'ramps', 'south', 'south16m', 'suns', 'wiltshire', \
        'wiltshire16m']
    description 'generate texture assets distributed with SkyControl'
}
task cleanTextures(type:Delete) {
    delete fileTree(dir: skies)
}

task clouds(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeClouds'
    outputs.files(["${skies}/clouds/clear.png", \
                   "${skies}/clouds/fbm.png", \
                   "${skies}/clouds/overcast.png"])
}
task debugEquator(type: JavaExec) {
    args = ['-c', '-p', 'equator']
    debug true
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
}
task equator(type: JavaExec) {
    args = ['-c', '-p', 'equator']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files(fileTree("${skies}/star-maps/equator"))
}
task equator16m(type: JavaExec) {
    args = ['-c', '-p', 'equator_16m']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files(fileTree("${skies}/star-maps/equator16m"))
}
task moons(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeMoons'
    outputs.files(fileTree("${skies}/moon-nonviral"))
}
task north(type: JavaExec) {
    args = ['-p', 'north']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/northern.png")
}
task north16m(type: JavaExec) {
    args = ['-p', 'north_16m']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/16m/northern.png")
}
task ramps(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeRamps'
    outputs.files("${skies}/ramps/haze.png")
}
task south(type: JavaExec) {
    args = ['-p', 'south']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/southern.png")
}
task south16m(type: JavaExec) {
    args = ['-p', 'south_16m']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/16m/southern.png")
}
task suns(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeSun'
    outputs.files(fileTree("${skies}/suns"))
}
task wiltshire(type: JavaExec) {
    args = ['-p', 'wiltshire']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/wiltshire.png")
}
task wiltshire16m(type: JavaExec) {
    args = ['-p', 'wiltshire_16m']
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("${skies}/star-maps/16m/wiltshire.png")
}
