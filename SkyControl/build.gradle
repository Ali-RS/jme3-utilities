// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

description = 'sky simulation and core library for jMonkeyEngine'
ext {
    artifact = 'SkyControl'
    version = '0.9.2'
    baseName = "${artifact}-${version}"
}

build { dependsOn 'pom' }
jar { baseName "${project.ext.baseName}" }
javadocJar { baseName "${project.ext.baseName}" }
processResources { dependsOn ':textures:textures' }
sourcesJar { baseName "${project.ext.baseName}" }

dependencies {
    compile "org.jmonkeyengine:jme3-core:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-bullet:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-effects:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-terrain:$jmonkeyengineVersion"
}

task pom {
    doLast {
        pom {
            project {
                artifactId "${project.ext.artifact}"
                licenses {
                    license {
                        distribution 'repo'
                        name 'New BSD (3-clause) License'
                        url 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                version "${project.ext.version}"
            }
        }.writeTo("${buildDir}/libs/${project.ext.baseName}.pom")
    }
}